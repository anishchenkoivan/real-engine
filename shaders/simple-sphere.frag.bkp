#version 460 core
out vec4 color;

uniform vec2 resolution;

vec2 fragCoord = gl_FragCoord.xy;

#define INFTY 1E9

#define COLOR_RED   0
#define COLOR_GREEN 1
#define COLOR_BLUE  2

struct Plane {
    float a;
    float b;
    float c;
    float d;
};

struct Ray {
    vec3 start;
    vec3 dir;
    int color;
};

struct Sphere {
    vec3 centre;
    float radius;
};

struct Reflection {
    Ray ray;
    float dist;
};

vec3 reflect(vec3 v, vec3 axis) {
    axis *= dot(axis, v);
    return (v - 2 * axis);
    return v - 2 * (v - axis);
}

bool isBlackSquare(float x) {
    return int((x + 0.5) * 55) % 2 == 0;
}

Reflection castRayWithPlane(Ray ray, Plane plane) {
    vec3 normal = vec3(plane.a, plane.b, plane.c);
    float denom = dot(normal, ray.dir);

    if (abs(denom) < 1e-6) {
        return Reflection(ray, INFTY);
    }

    float t = -(dot(normal, ray.start) + float(plane.d)) / denom;

    if (t < 0.0) {
        return Reflection(ray, INFTY);
    }

    vec3 intersection = normalize(ray.start + ray.dir * t);

    vec3 rvector = normal;
    vec3 refvector = reflect(ray.dir, rvector);
    return Reflection(Ray(intersection, refvector, ray.color), t * length(ray.dir));
}

float castRayWithSky(Ray ray) {
    if (ray.dir.y > -0.1) {
        if (ray.color == COLOR_BLUE) {
            return 0.3;
        } else {
            return 0.0;
        }
    }
    if (isBlackSquare(ray.dir.x) ^^ isBlackSquare(ray.dir.y)) {
        return 1.0;
    }
    return 0.0;
}

Reflection castRayWithSphere(Ray ray, Sphere sph) {
    vec3 OC = ray.start - sph.centre;
    float k1 = dot(ray.dir, ray.dir);
    float k2 = 2 * dot(OC, ray.dir);
    float k3 = dot(OC, OC) - sph.radius * sph.radius;
    float discr = k2 * k2 - 4 * k1 * k3;

    if (discr <= 0) {
        return Reflection(ray, INFTY);
    }

    discr = sqrt(discr);

    float t1 = (-k2 - discr) / (2 * k1);
    float t2 = (-k2 + discr) / (2 * k1);

    float t_min = min(t1, t2);
    float t_max = max(t1, t2);

    float t = t_min;

    if (t_min <= 1) {
        if (t_max <= 1) {
            return Reflection(ray, INFTY);
        }
        t = t_max;
    }

    vec3 intersection = ray.start + ray.dir * t;
    vec3 rvector = normalize(intersection - sph.centre);
    vec3 refvector = reflect(ray.dir, rvector);
    return Reflection(Ray(intersection, refvector, ray.color), t * length(ray.dir));
}

float castRay(Ray ray) {
    Reflection refl;
    Sphere sph1 = Sphere(vec3(-2.0, 1.0, 13.0), 1.5);
    Sphere sph2 = Sphere(vec3(1.0, -1.2, 13.0), 1.25);
    Plane pln1 = Plane(0.0, 1.0, 0.0, 0.0);

    for (int i = 0; i < 5; ++i) {
        Reflection refl1 = castRayWithSphere(ray, sph1);
        Reflection refl2 = castRayWithSphere(ray, sph2);

        if (refl1.dist < refl2.dist) {
            refl = refl1;
        } else {
            refl = refl2;
        }

        if (refl.dist == INFTY) {
            refl = castRayWithPlane(ray, pln1);
            if (refl.dist == INFTY) {
                return castRayWithSky(ray);
            }
        }
        ray = refl.ray;
    }

    return castRayWithSky(ray);
}

vec4 getColor(vec3 camera, vec3 dir) {
    vec4 res = vec4(0.0, 0.0, 0.0, 1.0);

    Ray ray = Ray(camera, dir, COLOR_RED);
    res.x = castRay(ray);

    ray.color = COLOR_GREEN;
    res.y = castRay(ray);

    ray.color = COLOR_BLUE;
    res.z = castRay(ray);

    return res;
}

void main() {
    fragCoord -= resolution / 2;
    float d = max(resolution.x, resolution.y);
    vec2 uv = fragCoord / d;
    vec3 camera = vec3(0.0, 0.0, -1.0);
    vec3 dir = normalize(vec3(uv, 0.0) - camera);
    color = getColor(camera, dir);
}
